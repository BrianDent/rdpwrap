REM START OF UPDATE_RDPWRAPINI.BAT
REM 20211111 BJD
REM THIS SCRIPT WILL DO THE FOLLOWING:
REM 1) CHECKS FOR NOW.EXE IN THE PATH
REM 2) VERIFIES IT IS RUNNING AS AN ADMINISTRATOR
REM 3) NET STOP UMRDPSERVICE
REM 4) TASKKILLS TERMSERVICE BECAUSE NET STOP SOMETIMES FAILS
REM 5) COPIES THE RDPWRAP.INI FILE TO THE RDP WRAPPER FOLDER
REM 6) NET START TERMSERVICE AND UMRDPSERVICE
REM 7) OPENS RDPCONF.EXE
REM
REM NOTES:
REM DOS ENVIRONMENT VARIABLES
REM %~dp0 IS THE DRIVE AND PATH
REM %~n0 IS THE NAME OF THE FILE
REM %~dp0%~n0 IS THE FULL PATH AND NAME
REM %~dp0%~n0.log IS C:\DIRECTORY\FILENAME.log
REM %DATE% %TIME% IS THE DATE AND TIME
REM FOR /F "TOKENS=*" %%G IN ('dir /s /b *.7z ^| findstr /i /v "WindowsImageBackup"') DO (
REM ECHO EXPAND %%~dpnG %%~tG>>%~dp0%~n0.log
REM
REM NOW.EXE IS FROM THE SERVER 2003 RESOURCE KIT WHICH IS DIFFICULT TO FIND
REM CALLING NOW MESSAGE | find /i "--" > %~DP0%~N0.LOG GIVES YOU DATED ECHOS
REM
REM SETLOCAL ENABLEDELAYEDEXPANSION FOR /F () LOOPS
REM SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION
REM UNNECESSARY BECAUSE WE ONLY LOOP ONCE BELOW
REM



REM FOR INTERACTIVE RUNS, DOESN'T MATTER FROM BATCH CALL
@ECHO ON



SET CLIENT=%computername%



REM DETERMINE IF NOW.EXE IS IN THE PATH OF THE BATCH FILE, DEFAULT=FALSE
REM FIND /I
REM 0 – THE STRING YOU WERE SEARCHING FOR WAS FOUND.
REM 1 – THE STRING YOU WERE SEARCHING FOR WAS NOT FOUND.
REM 2 – THIS MEANS YOU HAD A BAD SWITCH OR YOUR PARAMETERS WERE INCORRECT.
SET NOWEXISTS=FALSE
NOW TEST | FIND /I "--"
REM ERRORLEVEL=%ERRORLEVEL%
IF %ERRORLEVEL% EQU 0 (
  SET NOWEXISTS=TRUE
)



SET STARTINGDATE=%DATE%
IF "%STARTINGDATE:~2,1%" == "/" (
   SET MTH=%STARTINGDATE:~0,2%
   SET DAY=%STARTINGDATE:~3,2%
   SET YR=%STARTINGDATE:~6,4%
) ELSE (
   SET MTH=%STARTINGDATE:~4,2%
   SET DAY=%STARTINGDATE:~7,2%
   SET YR=%STARTINGDATE:~10,4%
)
SET HR=%TIME:~0,2%
SET HR0=%TIME:~0,1%
IF "%HR0%"==" " SET HR=0%TIME:~1,1%
SET MIN=%TIME:~6,2%
ECHO %STARTINGDATE% %YR%-%MTH%-%DAY%.%HR%:%MIN%



REM THIS SCRIPT REQUIRES ADMINISTRATIVE OR ELEVATED PRIVILEGES. MODIFIED FROM: 
REM HTTPS://STACKOVERFLOW.COM/QUESTIONS/7985755/HOW-TO-DETECT-IF-CMD-IS-RUNNING-AS-ADMINISTRATOR-HAS-ELEVATED-PRIVILEGES
REM AT IS DEPRECIATED, USE NET SESSION
NET SESSION > NUL
REM ECHO NOWEXISTS=%NOWEXISTS% ERRORLEVEL=%ERRORLEVEL%
IF %ERRORLEVEL% EQU 0 (
  IF "%NOWEXISTS%" EQU "TRUE" (
    NOW %CLIENT% -- YOU ARE RUNNING AS AN ADMINISTRATOR WITH ELEVATED PRIVILEGES | find /i "--"
    NOW %CLIENT% -- YOU ARE RUNNING AS AN ADMINISTRATOR WITH ELEVATED PRIVILEGES | find /i "--" > %~dp0%~n0.log
  ) ELSE (
    ECHO %STARTINGDATE% %HR%:%MIN% %CLIENT% YOU ARE RUNNING AS AN ADMINISTRATOR WITH ELEVATED PRIVILEGES
    ECHO %STARTINGDATE% %HR%:%MIN% %CLIENT% YOU ARE RUNNING AS AN ADMINISTRATOR WITH ELEVATED PRIVILEGES > %~dp0%~n0.log
  )
  GOTO :RUNASADMINISTRATOR
) ELSE (
  IF "%NOWEXISTS%" EQU "TRUE" (
    NOW %CLIENT% -- YOU ARE NOT RUNNING AS AN ADMINISTRATOR. EXITING IN 4 SECONDS ... | find /i "--"
    NOW %CLIENT% -- YOU ARE NOT RUNNING AS AN ADMINISTRATOR. EXITING IN 4 SECONDS ... | find /i "--" > %~dp0%~n0.log
  ) ELSE (
    ECHO %STARTINGDATE% %HR%:%MIN% %CLIENT% YOU ARE NOT RUNNING AS AN ADMINISTRATOR. EXITING IN 4 SECONDS ...
    ECHO %STARTINGDATE% %HR%:%MIN% %CLIENT% YOU ARE NOT RUNNING AS AN ADMINISTRATOR. EXITING IN 4 SECONDS ... > %~dp0%~n0.log
   )
  PING 127.0.0.1 > NUL 2>&1
    EXIT /B 1
)
:RUNASADMINISTRATOR



REM STOP UMRDPSERVICE AND TERMSERVICE, COPY RDPWRAP.INI AND RESTART
REM NET STOP UMRDPSERVICE ALWAYS STOPS
NET STOP UMRDPSERVICE

REM NET STOP TERMSERVICE WILL NOT ALWAYS STOP
REM NET STOP TERMSERVICE
REM LOOK IN THE TASKLIST FOR TERMSERVICE DLL, THERE IS ONLY ONE, KILL IT USING THE PID
FOR /F "TOKENS=1-3 SKIP=3 DELIMS= " %%A IN ('TASKLIST.EXE /M TERM*') DO (
  ECHO A=%%A, B=%%B, C=%%C
  TASKKILL /F /PID "%%B"
  GOTO :EXITTASKLIST
)
:EXITTASKLIST
IF "%NOWEXISTS%" EQU "TRUE" (
  NOW %CLIENT% -- TERMSERVICE WAS KILLED, RDP SESSIONS WILL BE TEMPORARILY DISCONNECTED | find /i "--" >> %~dp0%~n0.log
) ELSE (
  ECHO %STARTINGDATE% %HR%:%MIN% %CLIENT% TERMSERVICE WAS KILLED, RDP SESSIONS WILL BE TEMPORARILY DISCONNECTED >> %~dp0%~n0.log
)



REM %~DP0 EXPANDS TO THE DRIVE LETTER AND PATH OF THE BATCH FILE, IT CONTAINS A TRAILING \
COPY /Y "%~DP0RDPWRAP.INI" "%PROGRAMFILES%\RDP WRAPPER\RDPWRAP.INI"
NET START TERMSERVICE
NET START UMRDPSERVICE



REM OPEN RDPCONF.EXE TO SEE THE STATUS WITHOUT WAITING
REM %~DP0 EXPANDS TO THE DRIVE LETTER AND PATH OF THE BATCH FILE, IT CONTAINS A TRAILING \
START %~dp0rdpconf.exe



EXIT /B 0



REM END OF UPDATE_RDPWRAPINI.BAT
